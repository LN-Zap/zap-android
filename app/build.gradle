apply plugin: 'com.android.application'

// Needed for gRPC
apply plugin: 'com.google.protobuf'


android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "zapsolutions.zap"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 6
        versionName "0.2.3-alpha"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        setProperty("archivesBaseName", "zap-android-" + versionName)
    }
    buildTypes {
        debug {
            // with the following line we can install release and debug version simultaneously
            applicationIdSuffix ".debug"
            resValue "string", "app_name", "Zap Debug"
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            resValue "string", "app_name", "Zap"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'MissingTranslation'
    }

    // when referencing `armadillo` for encrypted SharedSettings without compileOptions we get error
    // Error: Invoke-customs are only supported starting with Android O (--min-api 26)
    // https://stackoverflow.com/questions/49891730/invoke-customs-are-only-supported-starting-with-android-0-min-api-26
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}


//  Needed for gRPC
sourceSets {
    main.java.srcDirs += "${protobuf.generatedFilesBaseDir}/main/javalite"
}


//  Needed for gRPC
protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.6.1"
    }
    plugins {
        javalite { artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0" }
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.21.0'
        }
    }

    generateProtoTasks {
        all().each { task ->
            task.plugins {
                javalite {}
                grpc {
                    // This will generate the ServiceGrpc
                    // Options added to --grpc_out
                    option 'lite'
                }
            }
        }
    }
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // encrypted SharedSettings
    implementation 'at.favre.lib:armadillo:0.7.0'

    // QR-Code scanner: ToDo: check if this is secure
    implementation 'me.dm7.barcodescanner:zbar:1.9.8'

    // QR-Code generator: ToDo: check if this is secure
    implementation 'com.github.kenglxn.QRGen:android:2.5.0'

    // Library to restart app
    implementation 'com.jakewharton:process-phoenix:2.0.0'

    // HTTP library to perform API interaction
    implementation 'com.android.volley:volley:1.1.1'

    // Bitcoin implementation   // conflicts with current protobuf implementation.
    // implementation 'org.bitcoinj:bitcoinj-core:0.14.7'

    // Library to detect when app goes to background or foreground
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0-alpha01'

    // gRPC implementation to communicate with lnd
    implementation 'io.grpc:grpc-okhttp:1.21.0'
    implementation 'io.grpc:grpc-protobuf-lite:1.21.0'
    implementation 'io.grpc:grpc-stub:1.21.0'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    compileOnly 'com.google.api.grpc:googleapis-common-protos:0.0.3'

    // Used for base encoding and URI escaping
    implementation 'com.google.guava:guava:28.0-android'


    implementation 'androidx.appcompat:appcompat:1.1.0-beta01'
    implementation 'com.google.android.material:material:1.1.0-alpha07'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.preference:preference:1.1.0-beta01'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
